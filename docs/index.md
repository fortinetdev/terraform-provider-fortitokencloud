---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "fortitokencloud Provider"
subcategory: ""
description: |-
  
---

# fortitokencloud Provider

## Example Usage

```terraform
terraform {
  required_providers {
    fortios = {
      source = "fortinetdev/fortios"
    }
    fortitokencloud = {
      source = "terraform-provider-fortitokencloud/fortitokencloud"
    }
  }
}

provider "fortitokencloud" {
  host = "https://ftc.fortinet.com:9696"
  clientid     = "<client id>"
  clientsecret = "<client secret>"
}

data "fortitokencloud_realm" "test" {
  name = "default"
}

resource "fortitokencloud_domain" "test" {
  name     = "hotmail.com"
  realm_id = data.fortitokencloud_realm.test.id
}

resource "fortitokencloud_usersource" "test" {
  name       = "terraform_azure"
  type       = "saml"
  realm_id   = data.fortitokencloud_realm.test.id
  entity_id  = "https://sts.windows.net/<tenant_id>/"
  login_url  = "https://login.microsoftonline.com/<tenant_id>/saml2"
  logout_url = "https://login.microsoftonline.com/<tenant_id>/saml2"
  domain_ids = [
    fortitokencloud_domain.test.id
  ]
  username_assertion = "username"
  attr_mapping = jsonencode({
    Username = "http://schemas.xmlsoap.org/ws/2005/05/identity/claims/emailaddress"
  })
}

resource "fortitokencloud_application" "test" {
  name = "fgt_sslvpn"
  # local
  realm_id = data.fortitokencloud_realm.test.id

  sp_entity_id = "https://<fgt_vpn_host>/remote/saml/metadata"
  sp_acs_url   = "https://<fgt_vpn_host>/remote/saml/login"
  sp_slo_url   = "https://<fgt_vpn_host>/remote/saml/logout"
  user_source_ids = [
    fortitokencloud_usersource.test.id
  ]
  ttl = 900
  attr_mapping = jsonencode({
    username = "Username"
  })
}

provider "fortios" {
  hostname = "<fgt mgmt ip>"
  token    = "<fgt api token>"
  insecure = "true"
}

resource "fortios_user_saml" "ftc_idp" {
  cert                   = "Fortinet_Factory"
  entity_id              = fortitokencloud_application.test.sp_entity_id
  idp_cert               = "REMOTE_Cert_1"
  idp_entity_id          = fortitokencloud_application.test.entity_id
  idp_single_logout_url  = fortitokencloud_application.test.slo_url
  idp_single_sign_on_url = fortitokencloud_application.test.sso_url
  name                   = "ftc_idp"
  single_logout_url      = fortitokencloud_application.test.sp_slo_url
  single_sign_on_url     = fortitokencloud_application.test.sp_acs_url
  user_name              = "username"
}

```



<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `clientid` (String)
- `clientsecret` (String, Sensitive)
- `host` (String)
